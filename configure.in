dnl $Id: configure.in,v 1.16 2003/03/24 09:05:38 chalky Exp $
dnl
dnl This source file is a part of the Synopsis Project.
dnl Copyright (C) 2001 Stefan Seefeld
dnl
dnl This library is free software; you can redistribute it and/or
dnl modify it under the terms of the GNU Library General Public
dnl License as published by the Free Software Foundation; either
dnl version 2 of the License, or (at your option) any later version.
dnl
dnl This library is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Library General Public License for more details.
dnl
dnl You should have received a copy of the GNU Library General Public
dnl License along with this library; if not, write to the
dnl Free Software Foundation, Inc., 675 Mass Ave, Cambridge,
dnl MA 02139, USA.
dnl
dnl Process this file with autoconf to produce a configure script.

dnl ------------------------------------------------------------------
dnl Autoconf initialization
dnl ------------------------------------------------------------------
AC_PREREQ(2.12)
AC_REVISION($Revision: 1.16 $)

AC_INIT(synopsis.spec.in)
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL

AC_ARG_WITH(python-prefix, [  --with-python-prefix    Prefix for python],[python_prefix="$withval"])
if test ".$python_prefix" != . ; then
	python_cppflags=-I$python_prefix/include
else
	python_prefix=/usr
fi

python_version=
AC_ARG_WITH(python-version, [  --with-python-version   Python version to use],[python_version="$withval"])

dnl
dnl check for python's C API
dnl
save_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$python_cppflags $CPPFLAGS"
if test ".$python_version" != .; then
	AC_CHECK_HEADER(python${python_version}/Python.h,[],
		 [AC_MSG_ERROR([Could not find valid python installation for version $python_version])])
fi
if test ".$python_version" = .; then
	AC_CHECK_HEADER(python2.2/Python.h, [python_version="2.2"])
fi
if test ".$python_version" = .; then
	AC_CHECK_HEADER(python2.1/Python.h, [python_version="2.1"])
fi
if test ".$python_version" = .; then
	AC_CHECK_HEADER(python2.0/Python.h, [python_version="2.0"])
fi
if test ".$python_version" = .; then
	AC_CHECK_HEADER(python1.6/Python.h, [python_version="1.6"])
fi
if test ".$python_version" = .; then
	AC_CHECK_HEADER(python1.5/Python.h, [python_version="1.5"])
fi
if test ".$python_version" = . ; then
	AC_MSG_ERROR(Could not find valid python installation !)
fi
CPPFLAGS="$save_CPPFLAGS"
python_include="<python${python_version}/Python.h>"
python_lib="-L${python_prefix}/lib/python${python_version}/config -lpython${python_version}"

AC_CHECK_FUNCS(openpty,, AC_CHECK_LIB(util,openpty, [AC_DEFINE(HAVE_OPENPTY)] [LIBS="$LIBS -lutil"]))
AC_ARG_ENABLE([gc], 
    [  --disable-gc            don't use garbage collector in C++ parser (default is enabled)],
    [use_gc=no],[use_gc=yes])

AC_SUBST(use_gc)
AC_SUBST(python_version)
AC_SUBST(python_prefix)
AC_SUBST(python_include)
AC_SUBST(python_lib)

dnl check for libdl
AC_CHECK_LIB(dl, dlopen, [LIBS="-ldl $LIBS"])

dnl Check host for things like -fPIC and library extension
AC_MSG_CHECKING([for library file extension])
cpp_profile=""
case `uname -s` in
CYGWIN*)
    library_ext=dll
    ;;
*)
    CPPFLAGS="-fPIC $CPPFLAGS"
    cpp_profile="-pg"
    library_ext=so
    ;;
esac
AC_MSG_RESULT([$library_ext])

AC_SUBST(library_ext)
AC_SUBST(cpp_profile)

if test "$use_gc" = yes; then
    dnl AC_CONFIG_SUBDIRS always runs configure... :(
    echo "Running configure in Synopsis/Parser/C++/gc ..."
    echo "-----------------------------------------------"
    popdir=`pwd`
    cd Synopsis/Parser/C++/gc
    ./configure
    cd $popdir
    echo "-----------------------------------------------"
    echo "- Configure for Garbage Collector was run with default options."
    echo "- Please rerun manually if required."
fi

AC_OUTPUT([
	local.mk:local.mk.in
	synopsis.spec:synopsis.spec.in
	Synopsis/Config.py:Synopsis/Config.py.in
])
